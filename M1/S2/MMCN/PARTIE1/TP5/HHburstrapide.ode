# Modification du modele de Hodgkin-Huxley
# sous-systeme rapide (HH lui-meme!!)

# EDOs
V'=(-IK-INa-IL+Iappl)/Cm
m'=(alpham(V)*(1-m)-betam(V)*m)
n'=(alphan(V)*(1-n)-betan(V)*n)
h'=(alphah(V)*(1-h)-betah(V)*h)

# Fonctions d'ouverture et fermeture pour les canaux ioniques
alpham(V)=0.1*(V+40)/(1-exp(-0.1*(V+40)))
alphah(V)=0.07*exp(-0.05*(V+65))
alphan(V)=0.01*(V+55)/(1-exp(-0.1*(V+55)))
betam(V)=4.0*exp(-0.0556*(V+65))
betah(V)=1.0/(1+exp(-0.1*(V+35)))
betan(V)=0.125*exp(-0.0125*(V+65))

# Courants ioniques
IK=gKbar*n*n*n*n*(V-VK)
INa=gNabar*m*m*m*h*(V-VNa)
IL=gLbar*(V-Vl)

# Quantites auxiliaires que l'on souhaite tracer
aux courantK=IK
aux courantNa=INa
aux courantFuite=IL
aux courantAppl=Iappl
aux var_nh=n+h

# Parametres
par Iappl=4
par gKbar=36,gNabar=120,gLbar=0.3,Cm=1
par VK=-77,VNa=50,VL=-54.4
par eps=0.01
par VR=-58

# Conditions initiales
init V=-59, n=0.1, m=0.41, h=0.38

# Resolution numerique
@ total=350, method=rk4, dt=0.01
@ xlo=0, xhi=350, ylo=-80, yhi=35
@ bounds=900000, maxstor=100000
# parametres de continuation
@ autoxmin=4, autoxmax=12, autoymin=-80, autoymax=45
@ nmax=500, npr=2, ntst=200,
@ ds=0.01, dsmin=1e-07, dsmax=0.1
@ parmin=-2, parmax=20, normmax=10000
@ epsl=1e-06, epsu=1e-05, epss=1e-05
done
